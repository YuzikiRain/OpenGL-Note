

## 理论



## BRDF

双向反射分布函数。

输入：入射（光）方向$\omega_i$，出射（观察）方向$\omega_o$，平面法线$n$以及一个用来表示微平面粗糙程度的参数$a$

输出：近似地求出每束光线对一个给定的材质属性的平面最终反射出来的光线的的贡献程度

比如，该函数对于一个完全光滑的表面（比如镜面），所有的入射光线$w_i$都会返回0.0，而只有与出射光线方向刚好相反的入射光线才返回1.0

BRDF基于我们之前所探讨过的微平面理论来近似的求得材质的反射与折射属性。对于一个BRDF，为了实现物理学上的可信度，它必须遵守能量守恒定律，也就是说反射光线的总和永远不能超过入射光线的总量。严格上来说，同样采用$\omega_i$和$\omega_o$作为输入参数的 Blinn-Phong光照模型也被认为是一个BRDF。然而由于Blinn-Phong模型并没有遵循能量守恒定律，因此它不被认为是基于物理的渲染。

### Cook-Torrance BRDF模型

$$f_r = k_d f_{lambert} +  k_s f_{cook-torrance}$$

$k_d$即$k_{diffuse}$：入射光线中**被折射**部分的能量所占的比率

$k_s$即$k_{specular}$：入射光线中**被反射**部分的比率

#### Lambertian漫反射

$$f_{lambert} = \frac{c}{\pi}$$

$c$表示表面颜色（回想一下漫反射表面纹理）。除以$\pi$是为了对漫反射光进行标准化，因为前面含有BRDF的积分方程是受$\pi$影响的（我们会在[IBL](https://learnopengl-cn.github.io/07 PBR/03 IBL/01 Diffuse irradiance/)的教程中探讨这个问题的）。

这个Lambertian漫反射类似我们之前经常使用的漫反射：之前我们是用表面法向量与光照方向向量进行点乘$n\cdot l$，然后再将结果与平面颜色相乘得到漫反射参数。点乘依然还在，但是却不在BRDF之内，而是转变成为了$L_o$积分末公式末尾处的$n\cdot\omega_i$。

#### 镜面反射

$$f_{cook-torrance} = \frac{DFG}{4(\omega_o \cdot n)(\omega_i \cdot n)}$$

-   **法线分布函数**(Normal **D**istribution Function)：估算在受到表面粗糙度的影响下，取向方向与中间向量一致的微平面的数量。这是用来估算微平面的主要函数。

-   **几何函数**(**G**eometry Function)：描述了微平面自成阴影的属性。当一个平面相对比较粗糙的时候，平面表面上的微平面有可能挡住其他的微平面从而减少表面所反射的光线。
-   **菲涅尔方程**(**F**resnel Rquation)：菲涅尔方程描述的是在不同的表面角下表面所反射的光线所占的比率。

##### 法线分布函数

法线分布函数$D$，或者说镜面分布，从统计学上近似的表示了**与某些（中间）向量$h$取向一致的微平面的比率**。

举例来说，假设给定向量$h$，如果我们的微平面中有35%与向量$h$取向一致，则法线分布函数将会返回0.35。

目前有很多种法线分布函数都可以从统计学上来估算微平面的总体取向度

**Trowbridge-Reitz GGX**：

$$NDF_{GGX TR}(n, h, \alpha) = \frac{\alpha^2}{\pi((n \cdot h)^2 (\alpha^2 - 1) + 1)^2}$$

-   $n$：法线向量
-   $h$：用来与平面上微平面做比较用的中间向量
-   $\alpha$：粗糙度roughness

粗糙度越小（也就是说表面越光滑），与中间向量取向一致的微平面会高度集中在一个越小的半径范围内，最终会生成一个越明亮的斑点。

粗糙度越大（微平面的取向方向会更加的随机），与中间向量取向一致的微平面会分布在一个越大的半径范围内，最终会生成一个越灰暗的斑点。

不同粗糙度的影响

![img](https://learnopengl-cn.github.io/img/07/01/ndf.png)

使用GLSL代码编写的Trowbridge-Reitz GGX法线分布函数

``` glsl
float D_GGX_TR(vec3 N, vec3 H, float a)
{
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom    = a2;
    float denom  = (NdotH2 * (a2 - 1.0) + 1.0);
    denom        = PI * denom * denom;

    return nom / denom;
}
```

##### 几何函数

几何函数从统计学上近似的求得了**微平面间相互遮蔽的比率**，这种相互遮蔽会损耗光线的能量。

![img](https://learnopengl-cn.github.io/img/07/01/geometry_shadowing.png)

我们将要使用的几何函数是GGX与Schlick-Beckmann近似的结合体，因此又称为**Schlick-GGX**：

$$G_{SchlickGGX}(n, v, k) = \frac{n \cdot v}{(n \cdot v)(1 - k) + k }$$

这里的$k$是$\alpha$基于几何函数针对直接光照或IBL光照的重映射(Remapping) 

$$k_{direct} = \frac{(\alpha + 1)^2}{8}$$

$$k_{IBL} = \frac{\alpha^2}{2}$$

注意，根据你的引擎把粗糙度转化为$\alpha$的方式不同，得到$\alpha$的值也有可能不同。

为了有效的估算几何部分，需要将观察方向（考虑了几何遮蔽(Geometry Obstruction)）和光线方向向量（考虑了几何阴影(Geometry Shadowing)）都考虑进去。我们可以使用史密斯法(Smith’s method)来把两者都纳入其中：

$$G(n, v, l, k) = G_{sub}(n, v, k) G_{sub}(n, l, k)$$

不同粗糙度的效果

![img](https://learnopengl-cn.github.io/img/07/01/geometry.png)

几何函数是一个值域为[0.0, 1.0]的乘数，其中白色或者说1.0表示没有微平面阴影，而黑色或者说0.0则表示微平面彻底被遮蔽。

使用GLSL编写的几何函数代码如下：

```glsl
float GeometrySchlickGGX(float NdotV, float k)
{
    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float k)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, k);
    float ggx2 = GeometrySchlickGGX(NdotL, k);

    return ggx1 * ggx2;
}
```

##### 菲涅尔方程

菲涅尔（发音为Freh-nel）方程描述的是**光线被反射与被折射的部分所占的比率**，这个比率会随着我们观察的角度不同而不同。

当光线碰撞到一个表面的时候，菲涅尔方程会根据观察角度告诉我们被反射的光线所占的百分比。利用这个反射比率和能量守恒原则，我们可以直接得出光线被折射的部分以及光线剩余的能量。

菲涅尔方程可以用Fresnel-Schlick近似法求得近似解：

$$F_{Schlick}(h, v, F_0) = F_0 + (1 - F_0) ( 1 - (h \cdot v))^5$$

-   $h$:表示
-   $v$:视角方向向量
-   $F_0$:表示平面的基础反射率，即正面朝向平面时的反射率。它是利用所谓**折射指数**(Indices of Refraction)或者说IOR计算得出的

菲涅尔方程还存在一些细微的问题。其中一个问题是Fresnel-Schlick近似仅仅对电介质或者说非金属表面有定义。对于导体(Conductor)表面（金属），使用它们的折射指数计算基础折射率并不能得出正确的结果，这样我们就需要使用一种不同的菲涅尔方程来对导体表面进行计算。由于这样很不方便，所以我们预先计算出平面对于法向入射（$F_0$）的反应（处于0度角，好像直接看向表面一样）然后基于相应观察角的Fresnel-Schlick近似对这个值进行插值，用这种方法来进行进一步的估算。这样我们就能对金属和非金属材质使用同一个公式了。

|       材料        |    $F_0$ (线性)    |    $F_0$ (sRGB)    |
| :---------------: | :----------------: | :----------------: |
|        水         | (0.02, 0.02, 0.02) | (0.15, 0.15, 0.15) |
|  塑料/玻璃（低）  | (0.03, 0.03, 0.03) | (0.21, 0.21, 0.21) |
|    塑料（高）     | (0.05, 0.05, 0.05) | (0.24, 0.24, 0.24) |
| 玻璃（高）/红宝石 | (0.08, 0.08, 0.08) | (0.31, 0.31, 0.31) |
|       钻石        | (0.17, 0.17, 0.17) | (0.45, 0.45, 0.45) |
|        铁         | (0.56, 0.57, 0.58) | (0.77, 0.78, 0.78) |
|        铜         | (0.95, 0.64, 0.54) | (0.98, 0.82, 0.76) |
|        金         | (1.00, 0.71, 0.29) | (1.00, 0.86, 0.57) |
|        铝         | (0.91, 0.92, 0.92) | (0.96, 0.96, 0.97) |
|        银         | (0.95, 0.93, 0.88) | (0.98, 0.97, 0.95) |

这里可以观察到的一个有趣的现象，所有电介质材质表面的基础反射率都不会高于0.17，这其实是例外而非普遍情况。导体材质表面的基础反射率起点更高一些并且（大多）在0.5和1.0之间变化。此外，对于导体或者金属表面而言基础反射率一般是带有色彩的，这也是为什么$F_0$要用RGB三原色来表示的原因（法向入射的反射率可随波长不同而不同）。这种现象我们**只能**在金属表面观察的到。

金属表面这些和电介质表面相比所独有的特性引出了所谓的**金属工作流**的概念。也就是我们需要额外使用一个被称为**金属度(Metalness)**的参数来参与编写表面材质。金属度用来描述一个材质表面是金属还是非金属的。

理论上来说，一个表面的金属度应该是二元的：要么是金属要么不是金属，不能两者皆是。但是，大多数的渲染管线都允许在0.0至1.0之间线性的调配金属度。这主要是由于材质纹理精度不足以描述一个拥有诸如细沙/沙状粒子/刮痕的金属表面。通过对这些小的类非金属粒子/刮痕调整金属度值，我们可以获得非常好看的视觉效果。

通过预先计算电介质与导体的$F_0$值，我们可以对两种类型的表面使用相同的Fresnel-Schlick近似，但是如果是金属表面的话就需要对基础反射率添加色彩。我们一般是按下面这个样子来实现的：

```glsl
vec3 F0 = vec3(0.04);
F0      = mix(F0, surfaceColor.rgb, metalness);
```

其中`cosTheta`是表面法向量$n$与观察方向$v$的点乘的结果。